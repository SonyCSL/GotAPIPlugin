
def app = false // if app=true then this project is application, else this project is library.
def commonArtifactId = 'gotapiplugin'

if(app) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion 21
    buildToolsVersion "23.0.0"

    if(app) {
        defaultConfig {
            applicationId "com.sonycsl.Kadecot.plugin.gotapi"
            minSdkVersion 15
            targetSdkVersion 21
            versionCode 1
            versionName "1.0"
        }
    } else {
        defaultConfig {
            minSdkVersion 15
            targetSdkVersion 21
            versionCode 1
            versionName "1.0"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    if(!app) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.aar')) {
                    def fileName = commonArtifactId + "-" + outputFile.name.toString()
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }
}
/////////////////////////////// Refer to local maven repositories ///////////////////////////////
repositories{
    flatDir {
        dir '../../wamp/app/build/outputs/aar'
    }
    flatDir {
        dir '../../kadecotcore/app/build/outputs/aar'
    }
}
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile project(':libraries')
    compile(name:'wamp-app-debug', ext:'aar')
    compile(name:'kadecotcore-app-debug', ext:'aar')

    compile 'org.apache.httpcomponents:httpmime:4.5'
    compile 'org.apache.httpcomponents:httpcore:4.4.1'
    compile 'org.apache.james:apache-mime4j-core:0.7.2'
    compile 'commons-codec:commons-codec:1.9'

}
